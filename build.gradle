plugins {
    id 'net.minecraftforge.gradle.forge' version '2.0.1'
    id 'com.matthewprenger.cursegradle' version '1.0.1'
}

group = 'com.matthewprenger.helpfixer'

if (System.getenv('BUILD_NUMBER') != null) {
    version += ".${System.getenv('BUILD_NUMBER')}"
} else {
    version += ".CUSTOM"
}

minecraft {
    version = "$minecraft_version-$forge_version"
    mappings = mappings_version
    runDir = 'run'
}

repositories {
    jcenter()
}

dependencies {
    testCompile 'junit:junit:4.12'
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    inputs.property 'description', project.description
    inputs.property 'url', project.url

    from(sourceSets.main.resources.srcDirs) {
        include '**/*.info'
        include '**/*.properties'

        expand([
                'version'    : project.version,
                'mcversion'  : project.minecraft.version,
                'description': project.description,
                'url'        : project.url
        ])
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
        exclude '**/*.properties'
    }
}

def commonManifest = [
        'Built-By'              : System.getProperty('user.name'),
        'Created-By'            : "${System.getProperty('java.version')} + (${System.getProperty('java.vendor')})",
        'Implementation-Version': project.version,
        'Implementation-Title'  : project.name,
        'Minecraft-Version'     : project.minecraft.version,
        'Forge-Version'         : forge_version
]

jar {
    manifest.mainAttributes(commonManifest)
}

task deobfJar(type: Jar) {
    manifest.mainAttributes(commonManifest)
    from sourceSets.main.output
    classifier = 'deobf'
}

tasks.assemble.dependsOn deobfJar

tasks.withType(JavaCompile) { task ->
    task.options.encoding = 'UTF-8'
    task.options.fork = true
}

curseforge {
    debug = true
    apiKey = project.hasProperty('curseforge_key') ? project.curseforge_key : ''
    project {
        id = '223797'
        changelog = project.hasProperty('changelog') ? project.changelog : ''
        releaseType = project.hasProperty('releaseType') ? project.releaseType : 'release'
        addGameVersion project.minecraft.version

        mainArtifact(jar) {
            displayName = "HelpFixer - $project.version"
        }

        addArtifact(sourceJar) {
            displayName = "HelpFixer Sources - $project.version"
        }

        addArtifact(deobfJar) {
            displayName = "HelpFixer Deobfuscated - $project.version"
        }
    }
}
