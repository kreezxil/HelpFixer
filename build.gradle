buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}

plugins {
//    id 'net.minecraftforge.gradle.forge' version '2.0.2'
    id 'com.matthewprenger.cursegradle' version '1.0.7'
}

apply plugin: 'net.minecraftforge.gradle.forge'

version = '1.3'
minecraft.version = '1.9.4-12.17.0.1908-1.9.4'
minecraft.mappings = 'snapshot_20160518'

repositories {
    jcenter()
}

dependencies {
    testCompile 'junit:junit:4.12'
}

minecraft {
    runDir = 'run'
}

if (System.getenv('BUILD_NUMBER') != null) {
    version += ".${System.getenv('BUILD_NUMBER')}"
} else {
    version += ".0"
}

[jar, sourceJar]*.version = "$minecraft.version-$project.version"

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    inputs.property 'description', project.description
    inputs.property 'url', project.url

    from(sourceSets.main.resources.srcDirs) {
        include '**/*.info'
        include '**/*.properties'

        expand([
                'version'    : project.version,
                'mcversion'  : project.minecraft.version,
                'description': project.description,
                'url'        : project.url
        ])
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
        exclude '**/*.properties'
    }
}

def commonManifest = [
        'Built-By'              : System.getProperty('user.name'),
        'Created-By'            : "${System.getProperty('java.version')} + (${System.getProperty('java.vendor')})",
        'Implementation-Version': project.version,
        'Implementation-Title'  : project.name,
        'Minecraft-Version'     : project.minecraft.version,
        'Forge-Version'         : project.minecraft.forgeVersion
]

jar {
    manifest.mainAttributes(commonManifest)
}

tasks.withType(JavaCompile) { task ->
    task.options.encoding = 'UTF-8'
    task.options.fork = true
}

curseforge {
    apiKey = project.hasProperty('curseApiKey') ? project.curseApiKey : ''
    project {
        id = '223797'
        changelog = project.hasProperty('changelog') ? project.changelog : ''
        releaseType = project.hasProperty('releaseType') ? project.releaseType : 'release'

        mainArtifact(jar) {
            displayName = "HelpFixer - $project.version"
        }

        addArtifact(sourceJar) {
            displayName = "HelpFixer Sources - $project.version"
        }
    }
}

if (project.hasProperty('changelog')) {
    println "Changelog: $project.changelog"
}
